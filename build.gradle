plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
    id "java-library"
    id "io.freefair.lombok" version "5.0.0-rc6"
}

group = "team.opay"
version = "0.1-SNAPSHOT"

repositories {
    mavenCentral()
}

def retrofitVersion = "2.8.1"
def okHttpVersion = "4.5.0"

dependencies {
    api "com.squareup.retrofit2:retrofit:$retrofitVersion"
    api "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    api "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    api "org.apache.logging.log4j:log4j-api:2.7"
    api "org.apache.logging.log4j:log4j-core:2.7"
    api "org.apache.logging.log4j:log4j-slf4j-impl:2.7"

    testImplementation "junit:junit:4.12"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testImplementation "com.google.truth:truth:1.0.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "fedache"
            name "Afe Oludare"
            email "fedachem@gmail.com"
        }
    }

    scm {
        url "https://github.com/fedache/cashier"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'team.opay'
            artifactId 'cashier'
            version '1.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java client for Cashier services')
                root.appendNode('name', 'cashier')
                root.appendNode('url', 'https://github.com/fedache/cashier')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'opay-repository'
        name = 'cahier'
        userOrg = ''
        licenses = ['MIT']
        vcsUrl = 'https://github.com/fedache/cashier'
        version {
            name = '0.1'
            desc = '0.1'
            released  = new Date()
        }
    }
}